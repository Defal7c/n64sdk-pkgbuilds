# Maintainer: Essem <smswessem@gmail.com>

_target=mips-n64
pkgname=${_target}-binutils
pkgver=2.30
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files (${_target})"
url="http://www.gnu.org/software/binutils/"
arch=('x86_64')
license=('GPL')
depends=('libelf')
source=("ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.xz")
sha256sums=('6e46b8aeae2f727a36f0bd9505e405768a72218f1796f0d09757d45209871ae6')

build() {
  cd binutils-${pkgver}

  INSTALL_PATH=/opt/crashsdk

  ./configure \
    --prefix="${INSTALL_PATH}" \
    --with-lib-path="${INSTALL_PATH}/lib" \
    --target=mips64-elf --with-arch=vr4300 \
    --program-prefix=mips-n64- \
    --enable-64-bit-bfd \
    --enable-plugins \
    --enable-shared \
    --disable-gold \
    --disable-multilib \
    --disable-nls \
    --disable-rpath \
    --disable-static \
    --disable-werror

  make -j$(nproc)
}

check() {
  cd binutils-${pkgver}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd binutils-${pkgver}
  make DESTDIR="$pkgdir" install-strip
}
